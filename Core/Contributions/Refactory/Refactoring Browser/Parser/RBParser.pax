| package |
package := Package name: 'RBParser'.
package paxVersion: 2;
	preDeclareClassesOnLoad: false;
	basicComment: 'Dolphin RBParser Package
Copyright John Brant and Don Roberts.

This package aliases most of the RBParser classes to the Dolphin equivalents which are then used instead. RB specific classes, such as the search/replace rules are hosted directly in this package.

Should be used in place of the full RBParser package, which contains the ''...parser for the Refactoring Browser. It contains both the ability to parse Smalltalk and also to rewrite Smalltalk code. The refactorings use this rewriting behavior to perform the source code transformations.'''.


package setClassNames: #(
	#{ParseTreeRewriter}
	#{ParseTreeSearcher}
	#{RBBlockReplaceRule}
	#{RBParseTreeRule}
	#{RBPatternBlockNode}
	#{RBPatternMessageNode}
	#{RBPatternMethodNode}
	#{RBPatternParser}
	#{RBPatternScanner}
	#{RBPatternVariableNode}
	#{RBPatternWrapperBlockNode}
	#{RBReplaceRule}
	#{RBSearchRule}
	#{RBSmallDictionary}
	#{RBStringReplacement}
	#{RBStringReplaceRule}
).

package setMethodNames: #(
	#(#{CompileFailedMethod} #refersToVariable:)
	#(#{RBConfigurableFormatter} #visitPatternWrapperBlockNode:)
	#(#{RBFormatter} #visitPatternWrapperBlockNode:)
	#(#{SmalltalkParser class} #parseRewriteExpression:)
	#(#{SmalltalkParser class} #parseRewriteExpression:onError:)
	#(#{SmalltalkParser class} #parseRewriteMethod:)
	#(#{SmalltalkParser class} #parseRewriteMethod:onError:)
	#(#{StAbstractVariableNode} #copyInContext:)
	#(#{StAbstractVariableNode} #equalTo:withMapping:)
	#(#{StAbstractVariableNode} #references:)
	#(#{StAbstractVariableNode} #replaceSourceFrom:)
	#(#{StAbstractVariableNode} #replaceSourceWith:)
	#(#{StAssignmentNode} #copyInContext:)
	#(#{StAssignmentNode} #equalTo:withMapping:)
	#(#{StAssignmentNode} #match:inContext:)
	#(#{StAssignmentNode} #replaceNode:withNode:)
	#(#{StAssignmentNode} #replaceSourceWith:)
	#(#{StBlockNode} #copyInContext:)
	#(#{StBlockNode} #equalTo:withMapping:)
	#(#{StBlockNode} #match:inContext:)
	#(#{StBlockNode} #references:)
	#(#{StBlockNode} #replaceNode:withNode:)
	#(#{StBraceArrayNode} #addNode:)
	#(#{StBraceArrayNode} #addNode:before:)
	#(#{StBraceArrayNode} #addNodeFirst:)
	#(#{StBraceArrayNode} #addNodes:)
	#(#{StBraceArrayNode} #addNodes:before:)
	#(#{StBraceArrayNode} #addNodesFirst:)
	#(#{StBraceArrayNode} #copyInContext:)
	#(#{StBraceArrayNode} #equalTo:withMapping:)
	#(#{StBraceArrayNode} #indexOfNode:)
	#(#{StBraceArrayNode} #match:inContext:)
	#(#{StBraceArrayNode} #references:)
	#(#{StBraceArrayNode} #replaceNode:withNode:)
	#(#{StCascadeNode} #copyInContext:)
	#(#{StCascadeNode} #equalTo:withMapping:)
	#(#{StCascadeNode} #match:inContext:)
	#(#{StCascadeNode} #replaceNode:withNode:)
	#(#{StLiteralArrayNode} #copyInContext:)
	#(#{StLiteralArrayNode} #equalTo:withMapping:)
	#(#{StLiteralArrayNode} #match:inContext:)
	#(#{StLiteralArrayNode} #replaceNode:withNode:)
	#(#{StLiteralNode} #replaceSourceFrom:)
	#(#{StLiteralNode} #replaceSourceWith:)
	#(#{StLiteralValueNode} #copyInContext:)
	#(#{StMessageNode} #copyInContext:)
	#(#{StMessageNode} #equalTo:withMapping:)
	#(#{StMessageNode} #isContainmentReplacement:)
	#(#{StMessageNode} #match:inContext:)
	#(#{StMessageNode} #replaceContainmentSourceWith:)
	#(#{StMessageNode} #replaceNode:withNode:)
	#(#{StMessageNode} #replaceSourceWith:)
	#(#{StMessageNode} #replaceSourceWithMessageNode:)
	#(#{StMethodNode} #addNode:)
	#(#{StMethodNode} #addReplacement:)
	#(#{StMethodNode} #addReturn)
	#(#{StMethodNode} #addSelfReturn)
	#(#{StMethodNode} #changeSourceSelectors:arguments:)
	#(#{StMethodNode} #clearReplacements)
	#(#{StMethodNode} #copyInContext:)
	#(#{StMethodNode} #equalTo:withMapping:)
	#(#{StMethodNode} #initialize)
	#(#{StMethodNode} #map:to:)
	#(#{StMethodNode} #mappingFor:)
	#(#{StMethodNode} #match:inContext:)
	#(#{StMethodNode} #newSource)
	#(#{StMethodNode} #references:)
	#(#{StMethodNode} #reformatSource)
	#(#{StMethodNode} #renameSelector:andArguments:)
	#(#{StMethodNode} #replaceNode:withNode:)
	#(#{StOptimizedNode} #copyInContext:)
	#(#{StOptimizedNode} #equalTo:withMapping:)
	#(#{StOptimizedNode} #match:inContext:)
	#(#{StOptimizedNode} #replaceNode:withNode:)
	#(#{StParseNodeVisitor} #visitPatternBlockNode:)
	#(#{StParseNodeVisitor} #visitPatternWrapperBlockNode:)
	#(#{StProgramNode} #addReplacement:)
	#(#{StProgramNode} #asReturn)
	#(#{StProgramNode} #clearReplacements)
	#(#{StProgramNode} #copyInContext:)
	#(#{StProgramNode} #copyList:inContext:)
	#(#{StProgramNode} #equalTo:withMapping:)
	#(#{StProgramNode} #mappingFor:)
	#(#{StProgramNode} #match:inContext:)
	#(#{StProgramNode} #matchList:against:inContext:)
	#(#{StProgramNode} #matchList:index:against:index:inContext:)
	#(#{StProgramNode} #references:)
	#(#{StProgramNode} #removeDeadCode)
	#(#{StProgramNode} #replaceMethodSource:)
	#(#{StProgramNode} #replaceNode:withNode:)
	#(#{StProgramNode} #replaceSourceFrom:)
	#(#{StProgramNode} #replaceSourceWith:)
	#(#{StProgramNode} #replaceWith:)
	#(#{StProgramNode} #selfMessages)
	#(#{StProgramNode} #superMessages)
	#(#{StReturnNode} #copyInContext:)
	#(#{StReturnNode} #equalTo:withMapping:)
	#(#{StReturnNode} #match:inContext:)
	#(#{StReturnNode} #replaceNode:withNode:)
	#(#{StSequenceNode} #addNode:)
	#(#{StSequenceNode} #addNode:before:)
	#(#{StSequenceNode} #addNodeFirst:)
	#(#{StSequenceNode} #addNodes:)
	#(#{StSequenceNode} #addNodes:before:)
	#(#{StSequenceNode} #addNodesFirst:)
	#(#{StSequenceNode} #addReturn)
	#(#{StSequenceNode} #addSelfReturn)
	#(#{StSequenceNode} #addTemporariesNamed:)
	#(#{StSequenceNode} #addTemporaryNamed:)
	#(#{StSequenceNode} #copyInContext:)
	#(#{StSequenceNode} #equalTo:withMapping:)
	#(#{StSequenceNode} #indexOfNode:)
	#(#{StSequenceNode} #match:inContext:)
	#(#{StSequenceNode} #references:)
	#(#{StSequenceNode} #removeDeadCode)
	#(#{StSequenceNode} #removeNode:)
	#(#{StSequenceNode} #removeTemporaryNamed:)
	#(#{StSequenceNode} #replaceNode:withNode:)
	#(#{StSequenceNode} #replaceNode:withNodes:)
).

package setVariableNames: #(
	#{RBAssignmentNode}
	#{RBAssignmentToken}
	#{RBBinarySelectorToken}
	#{RBBlockNode}
	#{RBCascadeNode}
	#{RBIdentifierToken}
	#{RBKeywordToken}
	#{RBLiteralArrayNode}
	#{RBLiteralArrayToken}
	#{RBLiteralNode}
	#{RBLiteralToken}
	#{RBLiteralValueNode}
	#{RBMessageNode}
	#{RBMethodNode}
	#{RBMultiKeywordLiteralToken}
	#{RBNumberLiteralToken}
	#{RBOptimizedNode}
	#{RBOptimizedToken}
	#{RBParser}
	#{RBProgramNode}
	#{RBProgramNodeVisitor}
	#{RBReturnNode}
	#{RBScanner}
	#{RBSequenceNode}
	#{RBSpecialCharacterToken}
	#{RBToken}
	#{RBValueNode}
	#{RBValueToken}
	#{RBVariableNode}
).

package setAliasVariableNames: #(
	#{RBAssignmentNode}
	#{RBAssignmentToken}
	#{RBBinarySelectorToken}
	#{RBBlockNode}
	#{RBCascadeNode}
	#{RBIdentifierToken}
	#{RBKeywordToken}
	#{RBLiteralArrayNode}
	#{RBLiteralArrayToken}
	#{RBLiteralNode}
	#{RBLiteralToken}
	#{RBLiteralValueNode}
	#{RBMessageNode}
	#{RBMethodNode}
	#{RBMultiKeywordLiteralToken}
	#{RBNumberLiteralToken}
	#{RBOptimizedNode}
	#{RBOptimizedToken}
	#{RBParser}
	#{RBProgramNode}
	#{RBProgramNodeVisitor}
	#{RBReturnNode}
	#{RBScanner}
	#{RBSequenceNode}
	#{RBSpecialCharacterToken}
	#{RBToken}
	#{RBValueNode}
	#{RBValueToken}
	#{RBVariableNode}
).

package setPrerequisites: #(
	'..\..\..\..\Object Arts\Dolphin\Base\Dolphin'
	'..\Formatters\RBFormatters'
	'..\..\..\..\Object Arts\Dolphin\System\Compiler\Smalltalk Parser'
).

package setManualPrerequisites: #(
	'Smalltalk Parser'
).

package!

"Class Definitions"!

Object subclass: #RBParseTreeRule
	instanceVariableNames: 'searchTree owner'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''
	classConstants: {}!
Object subclass: #RBStringReplacement
	instanceVariableNames: 'startPosition stopPosition string'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''
	classConstants: {}!
Collection subclass: #RBSmallDictionary
	instanceVariableNames: 'keys values size'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''
	classConstants: {}!
RBParseTreeRule subclass: #RBReplaceRule
	instanceVariableNames: 'verificationBlock'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''
	classConstants: {}!
RBParseTreeRule subclass: #RBSearchRule
	instanceVariableNames: 'answerBlock'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''
	classConstants: {}!
RBReplaceRule subclass: #RBBlockReplaceRule
	instanceVariableNames: 'replaceBlock'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''
	classConstants: {}!
RBReplaceRule subclass: #RBStringReplaceRule
	instanceVariableNames: 'replaceTree'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''
	classConstants: {}!
SmalltalkParser subclass: #RBPatternParser
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''
	classConstants: {}!
SmalltalkScanner subclass: #RBPatternScanner
	instanceVariableNames: ''
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''
	classConstants: {}!
StParseNodeVisitor subclass: #ParseTreeSearcher
	instanceVariableNames: 'searches answer argumentSearches context messages'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''
	classConstants: {}!
ParseTreeSearcher subclass: #ParseTreeRewriter
	instanceVariableNames: 'tree'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''
	classConstants: {}!
StMethodNode subclass: #RBPatternMethodNode
	instanceVariableNames: 'isList'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''
	classConstants: {}!
StVariableNode subclass: #RBPatternVariableNode
	instanceVariableNames: 'recurseInto isList isLiteral isStatement isAnything'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''
	classConstants: {}!
StBlockNode subclass: #RBPatternBlockNode
	instanceVariableNames: 'valueBlock'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''
	classConstants: {}!
RBPatternBlockNode subclass: #RBPatternWrapperBlockNode
	instanceVariableNames: 'wrappedNode'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''
	classConstants: {}!
StMessageNode subclass: #RBPatternMessageNode
	instanceVariableNames: 'isList isCascadeList'
	classVariableNames: ''
	poolDictionaries: ''
	classInstanceVariableNames: ''
	classConstants: {}!

"Variable Aliases"!

RBAssignmentNode := StAssignmentNode!

RBAssignmentToken := StAssignmentToken!

RBBinarySelectorToken := StBinarySelectorToken!

RBBlockNode := StBlockNode!

RBCascadeNode := StCascadeNode!

RBIdentifierToken := StIdentifierToken!

RBKeywordToken := StKeywordToken!

RBLiteralArrayNode := StLiteralArrayNode!

RBLiteralArrayToken := StLiteralArrayToken!

RBLiteralNode := StLiteralNode!

RBLiteralToken := StLiteralToken!

RBLiteralValueNode := StLiteralValueNode!

RBMessageNode := StMessageNode!

RBMethodNode := StMethodNode!

RBMultiKeywordLiteralToken := StMultiKeywordLiteralToken!

RBNumberLiteralToken := StNumberLiteralToken!

RBOptimizedNode := StOptimizedNode!

RBOptimizedToken := StOptimizedToken!

RBParser := SmalltalkParser!

RBProgramNode := StProgramNode!

RBProgramNodeVisitor := StParseNodeVisitor!

RBReturnNode := StReturnNode!

RBScanner := SmalltalkScanner!

RBSequenceNode := StSequenceNode!

RBSpecialCharacterToken := StSpecialCharacterToken!

RBToken := StToken!

RBValueNode := StValueNode!

RBValueToken := StValueToken!

RBVariableNode := StVariableNode!


"Loose Methods"!

!CompileFailedMethod methodsFor!

refersToVariable: aString 
	| tree searcher |
	tree := self parseTreeNoError.
	(tree isNil or: [(tree defines: aString) or: [tree body defines: aString]]) ifTrue: [^false].
	searcher := ParseTreeSearcher new.
	searcher
		matches: aString do: [:node :answer | true];
		matches: '[:`@vars | | `@temps | `@.Stmts]' do: [:node :answer | answer or: [((node defines: aString) or: [node body defines: aString]) not and: [searcher executeTree: node body initialAnswer: false]]].
	^searcher executeTree: self parseTree initialAnswer: false! !
!CompileFailedMethod categoriesFor: #refersToVariable:!development!private!testing! !

!RBConfigurableFormatter methodsFor!

visitPatternWrapperBlockNode: aRBPatternWrapperBlockNode
	self visitNode: aRBPatternWrapperBlockNode wrappedNode.
	codeStream nextPut: $`.
	self bracketWith: '{}' around: [self formatBlock: aRBPatternWrapperBlockNode]! !
!RBConfigurableFormatter categoriesFor: #visitPatternWrapperBlockNode:!public!visitor/double dispatching! !

!RBFormatter methodsFor!

visitPatternWrapperBlockNode: aRBPatternWrapperBlockNode
	self visitNode: aRBPatternWrapperBlockNode wrappedNode.
	self
		acceptBlockNode: aRBPatternWrapperBlockNode
		startBlockString: '`{'
		endBlockString: '}'! !
!RBFormatter categoriesFor: #visitPatternWrapperBlockNode:!public!visitor/double dispatching! !

!SmalltalkParser class methodsFor!

parseRewriteExpression: aString 
	^self parseRewriteExpression: aString onError: nil!

parseRewriteExpression: aString onError: aBlock 
	^RBPatternParser parseExpression: aString onError: aBlock!

parseRewriteMethod: aString 
	^self parseRewriteMethod: aString onError: nil!

parseRewriteMethod: aString onError: aBlock 
	^RBPatternParser parseMethod: aString onError: aBlock! !
!SmalltalkParser class categoriesFor: #parseRewriteExpression:!parsing!public! !
!SmalltalkParser class categoriesFor: #parseRewriteExpression:onError:!parsing!public! !
!SmalltalkParser class categoriesFor: #parseRewriteMethod:!parsing!public! !
!SmalltalkParser class categoriesFor: #parseRewriteMethod:onError:!parsing!public! !

!StAbstractVariableNode methodsFor!

copyInContext: aDictionary
	^self class named: self name!

equalTo: anObject withMapping: aDictionary
	^anObject isVariable and: [(aDictionary at: self name ifAbsentPut: [anObject name]) = anObject name].!

references: aVariableName 
	^self name = aVariableName!

replaceSourceFrom: aNode 
	self addReplacement: (RBStringReplacement 
				replaceFrom: aNode start
				to: aNode stop
				with: self name)!

replaceSourceWith: aNode
	self addReplacement: (RBStringReplacement 
				replaceFrom: self start
				to: self stop
				with: aNode formattedCode)! !
!StAbstractVariableNode categoriesFor: #copyInContext:!matching!public! !
!StAbstractVariableNode categoriesFor: #equalTo:withMapping:!comparing!public! !
!StAbstractVariableNode categoriesFor: #references:!public!testing! !
!StAbstractVariableNode categoriesFor: #replaceSourceFrom:!public!replacing! !
!StAbstractVariableNode categoriesFor: #replaceSourceWith:!public!replacing! !

!StAssignmentNode methodsFor!

copyInContext: aDictionary 
	^(self class new)
		variable: (variable copyInContext: aDictionary);
		value: (value copyInContext: aDictionary);
		yourself!

equalTo: anObject withMapping: aDictionary 
	^self class = anObject class and: 
			[(self variable equalTo: anObject variable withMapping: aDictionary) 
				and: [self value equalTo: anObject value withMapping: aDictionary]]!

match: aNode inContext: aDictionary 
	aNode class == self class ifFalse: [^false].
	^(variable match: aNode variable inContext: aDictionary)
		and: [value match: aNode value inContext: aDictionary]!

replaceNode: aNode withNode: anotherNode 
	value == aNode ifTrue: [self value: anotherNode].
	variable == aNode ifTrue: [self variable: anotherNode]!

replaceSourceWith: aNode 
	"Check if we are being replaced with a setter message send. If so, create the replacements to edit the original source."

	aNode isMessage ifFalse: [^super replaceSourceWith: aNode].
	aNode receiver isVariable ifFalse: [^super replaceSourceWith: aNode].
	aNode numArgs = 1 ifFalse: [^super replaceSourceWith: aNode].
	(self mappingFor: self value) = aNode arguments first 
		ifFalse: [^super replaceSourceWith: aNode].
	(self value hasParentheses not 
		and: [aNode arguments first precedence >= aNode precedence]) 
			ifTrue: 
				[self
					addReplacement: (RBStringReplacement 
								replaceFrom: self value start
								to: self value start - 1
								with: '(');
					addReplacement: (RBStringReplacement 
								replaceFrom: self value stop + 1
								to: self value stop
								with: ')')].
	self addReplacement: (RBStringReplacement 
				replaceFrom: self variable start
				to: self assignmentPosition + 1
				with: aNode receiver name , ' ' , aNode selector)! !
!StAssignmentNode categoriesFor: #copyInContext:!matching!public! !
!StAssignmentNode categoriesFor: #equalTo:withMapping:!comparing!public! !
!StAssignmentNode categoriesFor: #match:inContext:!matching!public! !
!StAssignmentNode categoriesFor: #replaceNode:withNode:!public!replacing! !
!StAssignmentNode categoriesFor: #replaceSourceWith:!public!replacing! !

!StBlockNode methodsFor!

copyInContext: aDictionary 
	^(self class new)
		arguments: (self copyList: arguments inContext: aDictionary);
		body: (body copyInContext: aDictionary);
		yourself!

equalTo: anObject withMapping: aDictionary 
	self class = anObject class ifFalse: [^false].
	self arguments size = anObject arguments size ifFalse: [^false].
	1 to: self arguments size
		do: 
			[:i | 
			((self arguments at: i) equalTo: (anObject arguments at: i)
				withMapping: aDictionary) ifFalse: [^false]].
	(self body equalTo: anObject body withMapping: aDictionary)
		ifFalse: [^false].
	self arguments do: [:each | aDictionary removeKey: each name].
	^true!

match: aNode inContext: aDictionary 
	aNode class == self class ifFalse: [^false].
	^(self 
		matchList: arguments
		against: aNode arguments
		inContext: aDictionary) 
			and: [body match: aNode body inContext: aDictionary]!

references: aVariableName 
	^body references: aVariableName!

replaceNode: aNode withNode: anotherNode 
	body == aNode ifTrue: [self body: anotherNode].
	self arguments: (arguments 
				collect: [:each | each == aNode ifTrue: [anotherNode] ifFalse: [each]])! !
!StBlockNode categoriesFor: #copyInContext:!matching!public! !
!StBlockNode categoriesFor: #equalTo:withMapping:!comparing!public! !
!StBlockNode categoriesFor: #match:inContext:!matching!public! !
!StBlockNode categoriesFor: #references:!public!testing! !
!StBlockNode categoriesFor: #replaceNode:withNode:!public!replacing! !

!StBraceArrayNode methodsFor!

addNode: aNode
	statements := statements asOrderedCollection
				add: aNode;
				yourself.
	aNode parent: self.
	^aNode!

addNode: aNode before: anotherNode
	| index |
	aNode isReturn ifTrue: [self error: 'Cannot add return node'].
	index := self indexOfNode: anotherNode.
	index == 0 ifTrue: [^self addNode: aNode].
	statements := statements asOrderedCollection
				add: aNode beforeIndex: index;
				yourself.
	aNode parent: self.
	^aNode!

addNodeFirst: aNode
	statements := statements asOrderedCollection
				addFirst: aNode;
				yourself.
	aNode parent: self.
	^aNode!

addNodes: aCollection
	statements := statements asOrderedCollection
				addAll: aCollection;
				yourself.
	aCollection do: [:each | each parent: self].
	^aCollection!

addNodes: aCollection before: anotherNode
	aCollection do: [:each | self addNode: each before: anotherNode].
	^aCollection!

addNodesFirst: aCollection
	statements := statements asOrderedCollection
				addAllFirst: aCollection;
				yourself.
	aCollection do: [:each | each parent: self].
	^aCollection!

copyInContext: aDictionary
	^self class statements: (self copyList: statements inContext: aDictionary)!

equalTo: anObject withMapping: aDictionary
	self class = anObject class ifFalse: [^false].
	self statements size = anObject statements size ifFalse: [^false].
	self statements with: anObject statements
		do: [:first :second | (first equalTo: second withMapping: aDictionary) ifFalse: [^false]].
	aDictionary values asSet size = aDictionary size ifFalse: [^false].
	^true!

indexOfNode: aNode
	"Try to find the node by first looking for ==, and then for ="

	| index |
	(index := statements identityIndexOf: aNode) == 0 ifTrue: [index := statements indexOf: aNode].
	^index!

match: aNode inContext: aDictionary
	aNode class = self class ifFalse: [^false].
	^self
		matchList: statements
		against: aNode statements
		inContext: aDictionary!

references: aVariableName 
	^statements anySatisfy: [:each | each references: aVariableName]!

replaceNode: aNode withNode: anotherNode
	self statements: (statements collect: [:each | each == aNode ifTrue: [anotherNode] ifFalse: [each]])! !
!StBraceArrayNode categoriesFor: #addNode:!adding nodes!public! !
!StBraceArrayNode categoriesFor: #addNode:before:!adding nodes!public! !
!StBraceArrayNode categoriesFor: #addNodeFirst:!adding nodes!public! !
!StBraceArrayNode categoriesFor: #addNodes:!adding nodes!public! !
!StBraceArrayNode categoriesFor: #addNodes:before:!adding nodes!public! !
!StBraceArrayNode categoriesFor: #addNodesFirst:!adding nodes!public! !
!StBraceArrayNode categoriesFor: #copyInContext:!matching!public! !
!StBraceArrayNode categoriesFor: #equalTo:withMapping:!comparing!public! !
!StBraceArrayNode categoriesFor: #indexOfNode:!helpers!private! !
!StBraceArrayNode categoriesFor: #match:inContext:!matching!public! !
!StBraceArrayNode categoriesFor: #references:!public!testing! !
!StBraceArrayNode categoriesFor: #replaceNode:withNode:!public!replacing! !

!StCascadeNode methodsFor!

copyInContext: aDictionary 
	^(self class new)
		messages: (self copyList: messages inContext: aDictionary);
		yourself!

equalTo: anObject withMapping: aDictionary 
	self class = anObject class ifFalse: [^false].
	self messages size == anObject messages size ifFalse: [^false].
	1 to: self messages size
		do: 
			[:i | 
			((self messages at: i) equalTo: (anObject messages at: i)
				withMapping: aDictionary) ifFalse: [^false]].
	^true!

match: aNode inContext: aDictionary 
	aNode class == self class ifFalse: [^false].
	^self 
		matchList: messages
		against: aNode messages
		inContext: aDictionary!

replaceNode: aNode withNode: anotherNode 
	self messages: (messages 
				collect: [:each | each == aNode ifTrue: [anotherNode] ifFalse: [each]])! !
!StCascadeNode categoriesFor: #copyInContext:!matching!public! !
!StCascadeNode categoriesFor: #equalTo:withMapping:!comparing!public! !
!StCascadeNode categoriesFor: #match:inContext:!matching!public! !
!StCascadeNode categoriesFor: #replaceNode:withNode:!public!replacing! !

!StLiteralArrayNode methodsFor!

copyInContext: aDictionary 
	^self class 
		startPosition: nil
		contents: (self copyList: contents inContext: aDictionary)
		stopPosition: nil
		isByteArray: isByteArray!

equalTo: anObject withMapping: aDictionary 
	self class = anObject class ifFalse: [^false].
	self isForByteArray = anObject isForByteArray ifFalse: [^false].
	self contents size = anObject contents size ifFalse: [^false].
	1 to: self contents size
		do: 
			[:i | 
			((self contents at: i) equalTo: (anObject contents at: i) withMapping: aDictionary) 
				ifFalse: [^false]].
	^true!

match: aNode inContext: aDictionary 
	aNode class == self class ifFalse: [^false].
	self isForByteArray = aNode isForByteArray ifFalse: [^false].
	^self 
		matchList: contents
		against: aNode contents
		inContext: aDictionary!

replaceNode: aNode withNode: anotherNode 
	self contents: (contents 
				collect: [:each | each == aNode ifTrue: [anotherNode] ifFalse: [each]])! !
!StLiteralArrayNode categoriesFor: #copyInContext:!matching!public! !
!StLiteralArrayNode categoriesFor: #equalTo:withMapping:!comparing!public! !
!StLiteralArrayNode categoriesFor: #match:inContext:!matching!public! !
!StLiteralArrayNode categoriesFor: #replaceNode:withNode:!public!replacing! !

!StLiteralNode methodsFor!

replaceSourceFrom: aNode 
	self addReplacement: (RBStringReplacement 
				replaceFrom: aNode start
				to: aNode stop
				with: self formattedCode)!

replaceSourceWith: aNode 
	self addReplacement: (RBStringReplacement 
				replaceFrom: self start
				to: self stop
				with: aNode formattedCode)! !
!StLiteralNode categoriesFor: #replaceSourceFrom:!private!replacing! !
!StLiteralNode categoriesFor: #replaceSourceWith:!private!replacing! !

!StLiteralValueNode methodsFor!

copyInContext: aDictionary
	^self class literalToken: token removePositions! !
!StLiteralValueNode categoriesFor: #copyInContext:!matching!public! !

!StMessageNode methodsFor!

copyInContext: aDictionary 
	^(self class new)
		receiver: (receiver copyInContext: aDictionary);
		selectorParts: (selectorParts collect: [:each | each removePositions]);
		arguments: (arguments collect: [:each | each copyInContext: aDictionary]);
		yourself!

equalTo: anObject withMapping: aDictionary 
	self class = anObject class ifFalse: [^false].
	((self receiver equalTo: anObject receiver withMapping: aDictionary)
		and: [self selector = anObject selector]) ifFalse: [^false].
	1 to: self arguments size
		do: 
			[:i | 
			((self arguments at: i) equalTo: (anObject arguments at: i)
				withMapping: aDictionary) ifFalse: [^false]].
	^true!

isContainmentReplacement: aNode 
	^(self mappingFor: self receiver) = aNode 
		or: [self arguments anySatisfy: [:each | (self mappingFor: each) = aNode]]!

match: aNode inContext: aDictionary 
	aNode class == self class ifFalse: [^false].
	self selector == aNode selector ifFalse: [^false].
	(receiver match: aNode receiver inContext: aDictionary) ifFalse: [^false].
	1 to: arguments size
		do: 
			[:i | 
			((arguments at: i) match: (aNode arguments at: i) inContext: aDictionary)
				ifFalse: [^false]].
	^true!

replaceContainmentSourceWith: aNode 
	| originalNode needsParenthesis |
	needsParenthesis := aNode hasParentheses not and: [aNode needsParenthesis].
	originalNode := (self mappingFor: self receiver) = aNode 
				ifTrue: [self receiver]
				ifFalse: [self arguments detect: [:each | (self mappingFor: each) = aNode]].
	self
		addReplacement: (RBStringReplacement 
					replaceFrom: self start
					to: originalNode start - 1
					with: (needsParenthesis ifTrue: ['('] ifFalse: ['']));
		addReplacement: (RBStringReplacement 
					replaceFrom: originalNode stop + 1
					to: self stop
					with: (needsParenthesis ifTrue: [')'] ifFalse: ['']))!

replaceNode: aNode withNode: anotherNode 
	"If we're inside a cascade node and are changing the receiver, change all the receivers"

	receiver == aNode 
		ifTrue: 
			[self receiver: anotherNode.
			(parent notNil and: [parent isCascade]) 
				ifTrue: [parent messages do: [:each | each receiver: anotherNode]]].
	self arguments: (arguments 
				collect: [:each | each == aNode ifTrue: [anotherNode] ifFalse: [each]])!

replaceSourceWith: aNode 
	(self isContainmentReplacement: aNode) 
		ifTrue: [^self replaceContainmentSourceWith: aNode].
	aNode isMessage ifFalse: [^super replaceSourceWith: aNode].
	^self replaceSourceWithMessageNode: aNode!

replaceSourceWithMessageNode: aNode 
	| isBinaryToKeyword |
	self numArgs = aNode numArgs ifFalse: [^super replaceSourceWith: aNode].
	(self mappingFor: self receiver) = aNode receiver 
		ifFalse: [^super replaceSourceWith: aNode].
	self arguments with: aNode arguments
		do: [:old :new | (self mappingFor: old) = new ifFalse: [^super replaceSourceWith: aNode]].
	(isBinaryToKeyword := self isBinary and: [aNode isKeyword]) 
		ifTrue: 
			[(self hasParentheses not and: [self parent precedence <= aNode precedence]) 
				ifTrue: 
					[self
						addReplacement: (RBStringReplacement 
									replaceFrom: self start
									to: self start - 1
									with: '(');
						addReplacement: (RBStringReplacement 
									replaceFrom: self stop + 1
									to: self stop
									with: ')')]].
	self selectorParts with: aNode selectorParts
		do: 
			[:old :new | 
			old value ~= new value 
				ifTrue: 
					[self addReplacement: (RBStringReplacement 
								replaceFrom: old start
								to: old stop
								with: ((isBinaryToKeyword 
										and: [(self source at: old start - 1) isSeparator not]) 
											ifTrue: [' ' , new value]
											ifFalse: [new value]))]]! !
!StMessageNode categoriesFor: #copyInContext:!matching!public! !
!StMessageNode categoriesFor: #equalTo:withMapping:!comparing!public! !
!StMessageNode categoriesFor: #isContainmentReplacement:!private!replacing! !
!StMessageNode categoriesFor: #match:inContext:!matching!public! !
!StMessageNode categoriesFor: #replaceContainmentSourceWith:!private!replacing! !
!StMessageNode categoriesFor: #replaceNode:withNode:!public!replacing! !
!StMessageNode categoriesFor: #replaceSourceWith:!private!replacing! !
!StMessageNode categoriesFor: #replaceSourceWithMessageNode:!private!replacing! !

!StMethodNode methodsFor!

addNode: aNode 
	^body addNode: aNode!

addReplacement: aStringReplacement 
	replacements isNil ifTrue: [^self].
	replacements add: aStringReplacement!

addReturn
	body addReturn!

addSelfReturn
	^body addSelfReturn!

changeSourceSelectors: oldSelectorParts arguments: oldArguments 
	
	[oldSelectorParts size = selectorParts size ifFalse: [^self].
	oldArguments size = arguments size ifFalse: [^self].
	oldSelectorParts with: selectorParts
		do: 
			[:old :new | 
			self addReplacement: (RBStringReplacement 
						replaceFrom: old start
						to: old stop
						with: new value)].
	oldArguments with: arguments
		do: 
			[:old :new | 
			self addReplacement: (RBStringReplacement 
						replaceFrom: old start
						to: old stop
						with: new value)]] 
			on: Error
			do: [:ex | ex return]!

clearReplacements
	replacements := nil!

copyInContext: aDictionary 
	^(self class new)
		selectorParts: (selectorParts collect: [:each | each removePositions]);
		arguments: (arguments collect: [:each | each copyInContext: aDictionary]);
		body: (body copyInContext: aDictionary);
		source: (aDictionary at: '-source-');
		yourself!

equalTo: anObject withMapping: aDictionary 
	self class = anObject class ifFalse: [^false].
	(self selector = anObject selector 
		and: [self body equalTo: anObject body withMapping: aDictionary]) 
			ifFalse: [^false].
	1 to: self arguments size
		do: 
			[:i | 
			((self arguments at: i) equalTo: (anObject arguments at: i)
				withMapping: aDictionary) ifFalse: [^false].
			aDictionary removeKey: (self arguments at: i) name].
	^self tag = anObject tag!

initialize
	replacements := SortedCollection sortBlock: 
					[:a :b | 
					a startPosition < b startPosition 
						or: [a startPosition = b startPosition and: [a stopPosition < b stopPosition]]]!

map: oldNode to: newNode
	(nodeReplacements ifNil: [nodeReplacements := IdentityDictionary new]) at: oldNode put: newNode!

mappingFor: oldNode
	^nodeReplacements ifNil: [oldNode] ifNotNil: [nodeReplacements at: oldNode ifAbsent: [oldNode]]!

match: aNode inContext: aDictionary 
	self class == aNode class ifFalse: [^false].
	aDictionary at: '-source-' put: aNode source.
	self selector == aNode selector ifFalse: [^false].
	^(self 
		matchList: arguments
		against: aNode arguments
		inContext: aDictionary) 
			and: [body match: aNode body inContext: aDictionary]!

newSource
	replacements isNil ifTrue: [^self formattedCode].
	^[self reformatSource] on: Error do: [:ex | ex return: self formattedCode]!

references: aVariableName 
	^body references: aVariableName!

reformatSource
	| newSource stream |
	stream := WriteStream on: (Utf8String new: source size + 100).
	stream 
		nextPutAll: (source copyFrom: (replacements inject: 1
						into: 
							[:sum :each | 
							stream
								nextPutAll: (source copyFrom: sum to: each startPosition - 1);
								nextPutAll: each string.
							each stopPosition + 1])
				to: source size).
	newSource := stream contents.
	self 
		= (RBParser parseMethod: newSource onError: [:s :p | ^self formattedCode]) 
			ifFalse: [^self formattedCode].	"Sanity check -- make sure the formatted code is = self"
	^newSource!

renameSelector: newSelector andArguments: varNodeCollection 
	| oldSelectorParts oldArguments |
	oldSelectorParts := selectorParts.
	oldArguments := arguments.
	self
		arguments: varNodeCollection;
		selector: newSelector.
	self changeSourceSelectors: oldSelectorParts arguments: oldArguments!

replaceNode: aNode withNode: anotherNode 
	aNode == body ifTrue: [self body: anotherNode].
	self arguments: (arguments 
				collect: [:each | each == aNode ifTrue: [anotherNode] ifFalse: [each]])! !
!StMethodNode categoriesFor: #addNode:!accessing!public! !
!StMethodNode categoriesFor: #addReplacement:!public!replacing! !
!StMethodNode categoriesFor: #addReturn!accessing!public! !
!StMethodNode categoriesFor: #addSelfReturn!accessing!public! !
!StMethodNode categoriesFor: #changeSourceSelectors:arguments:!private!replacing! !
!StMethodNode categoriesFor: #clearReplacements!public!replacing! !
!StMethodNode categoriesFor: #copyInContext:!matching!public! !
!StMethodNode categoriesFor: #equalTo:withMapping:!comparing!public! !
!StMethodNode categoriesFor: #initialize!initializing!public! !
!StMethodNode categoriesFor: #map:to:!public!replacing! !
!StMethodNode categoriesFor: #mappingFor:!public!replacing! !
!StMethodNode categoriesFor: #match:inContext:!matching!public! !
!StMethodNode categoriesFor: #newSource!accessing!public! !
!StMethodNode categoriesFor: #references:!public!testing! !
!StMethodNode categoriesFor: #reformatSource!operations!private! !
!StMethodNode categoriesFor: #renameSelector:andArguments:!accessing!public! !
!StMethodNode categoriesFor: #replaceNode:withNode:!public!replacing! !

!StOptimizedNode methodsFor!

copyInContext: aDictionary 
	^self class body: (body copyInContext: aDictionary)!

equalTo: anObject withMapping: aDictionary 
	^self class = anObject class 
		and: [self body equalTo: anObject body withMapping: aDictionary]!

match: aNode inContext: aDictionary 
	aNode class == self class ifFalse: [^false].
	^body match: aNode body inContext: aDictionary!

replaceNode: aNode withNode: anotherNode 
	body == aNode ifTrue: [self body: anotherNode]! !
!StOptimizedNode categoriesFor: #copyInContext:!matching!public! !
!StOptimizedNode categoriesFor: #equalTo:withMapping:!comparing!public! !
!StOptimizedNode categoriesFor: #match:inContext:!matching!public! !
!StOptimizedNode categoriesFor: #replaceNode:withNode:!public!replacing! !

!StParseNodeVisitor methodsFor!

visitPatternBlockNode: aRBPatternBlockNode
	self visitArgumentNodes: aRBPatternBlockNode arguments.
	self visitNode: aRBPatternBlockNode body!

visitPatternWrapperBlockNode: aRBPatternWrapperBlockNode
	self visitNode: aRBPatternWrapperBlockNode wrappedNode.
	self visitArgumentNodes: aRBPatternWrapperBlockNode arguments.
	self visitNode: aRBPatternWrapperBlockNode body! !
!StParseNodeVisitor categoriesFor: #visitPatternBlockNode:!public!visiting! !
!StParseNodeVisitor categoriesFor: #visitPatternWrapperBlockNode:!public!visiting! !

!StProgramNode methodsFor!

addReplacement: aStringReplacement
	parent isNil ifTrue: [^self].
	parent addReplacement: aStringReplacement!

asReturn
	"Change the current node to a return node."

	parent isNil 
		ifTrue: [self error: 'Cannot change to a return without a parent node.'].
	parent isSequence 
		ifFalse: [self error: 'Parent node must be a sequence node.'].
	(parent isLast: self) ifFalse: [self error: 'Return node must be last.'].
	^parent addReturn!

clearReplacements
	parent isNil ifTrue: [^self].
	parent clearReplacements!

copyInContext: aDictionary
	^self copy!

copyList: matchNodes inContext: aDictionary 
	| newNodes |
	newNodes := OrderedCollection new.
	matchNodes do: 
			[:each | 
			| object |
			object := each copyInContext: aDictionary.
			newNodes addAll: object].
	^newNodes!

equalTo: aNode withMapping: aDictionary 
	^self = aNode!

mappingFor: aNode
	| method |
	method := self methodNode.
	method isNil ifTrue: [^aNode].
	^method mappingFor: aNode!

match: aNode inContext: aDictionary 
	^self = aNode!

matchList: matchNodes against: programNodes inContext: aDictionary 
	^self
		matchList: matchNodes
		index: 1
		against: programNodes
		index: 1
		inContext: aDictionary!

matchList: matchNodes index: matchIndex against: programNodes index: programIndex inContext: aDictionary 
	| node |
	matchNodes size < matchIndex ifTrue: [^programNodes size < programIndex].
	node := matchNodes at: matchIndex.
	node isList 
		ifTrue: 
			[| currentIndex |
			currentIndex := programIndex - 1.
			
			[| currentDictionary |
			currentDictionary := aDictionary copy.
			programNodes size < currentIndex or: 
					[| nodes |
					nodes := programNodes copyFrom: programIndex to: currentIndex.
					(currentDictionary at: node ifAbsentPut: [nodes]) = nodes and: 
							[(self 
								matchList: matchNodes
								index: matchIndex + 1
								against: programNodes
								index: currentIndex + 1
								inContext: currentDictionary) 
									ifTrue: 
										[currentDictionary keysAndValuesDo: [:key :value | aDictionary at: key put: value].
										^true].
							false]]] 
					whileFalse: [currentIndex := currentIndex + 1].
			^false].
	programNodes size < programIndex ifTrue: [^false].
	(node match: (programNodes at: programIndex) inContext: aDictionary) ifFalse: [^false].
	^self 
		matchList: matchNodes
		index: matchIndex + 1
		against: programNodes
		index: programIndex + 1
		inContext: aDictionary!

references: aVariableName 
	self childrenDo: [:each | (each references: aVariableName) ifTrue: [^true]].
	^false!

removeDeadCode
	self childrenDo: [:each | each removeDeadCode]!

replaceMethodSource: aNode 
	"We are being replaced with aNode -- if possible try to perform an in place edit of the source."

	| method |
	method := self methodNode.
	method notNil ifTrue: [method map: self to: aNode].
	aNode parent: self parent.
	[self replaceSourceWith: aNode] on: Error
		do: 
			[:ex | 
			self clearReplacements.
			ex return]!

replaceNode: aNode withNode: anotherNode 
	self error: 'I don''t store other nodes'!

replaceSourceFrom: aNode 
	self clearReplacements!

replaceSourceWith: aNode 
	aNode replaceSourceFrom: self!

replaceWith: aNode 
	parent isNil ifTrue: [self error: 'This node doesn''t have a parent'].
	self replaceMethodSource: aNode.
	parent replaceNode: self withNode: aNode!

selfMessages
	| searcher |
	searcher := ParseTreeSearcher new.
	searcher matches: 'self `@msg: ``@args'
		do: 
			[:aNode :answer | 
			answer
				add: aNode selector;
				yourself].
	^searcher executeTree: self initialAnswer: Set new!

superMessages
	| searcher |
	SuperSearch isNil
		ifTrue: 
			[SuperSearch := RBParser parseRewriteExpression: 'super `@msg: ``@args'.
			SuperSearch
				selector;
				isImmutable: true].
	searcher := ParseTreeSearcher new.
	searcher addRule: (RBSearchRule searchForTree: SuperSearch
				thenDo: 
					[:aNode :answer |
					answer
						add: aNode selector;
						yourself]).
	^searcher executeTree: self initialAnswer: Set new! !
!StProgramNode categoriesFor: #addReplacement:!public!replacing! !
!StProgramNode categoriesFor: #asReturn!accessing!public! !
!StProgramNode categoriesFor: #clearReplacements!public!replacing! !
!StProgramNode categoriesFor: #copyInContext:!matching!public! !
!StProgramNode categoriesFor: #copyList:inContext:!matching!public! !
!StProgramNode categoriesFor: #equalTo:withMapping:!comparing!public! !
!StProgramNode categoriesFor: #mappingFor:!accessing!public! !
!StProgramNode categoriesFor: #match:inContext:!matching!public! !
!StProgramNode categoriesFor: #matchList:against:inContext:!matching!public! !
!StProgramNode categoriesFor: #matchList:index:against:index:inContext:!matching!public! !
!StProgramNode categoriesFor: #references:!public!testing! !
!StProgramNode categoriesFor: #removeDeadCode!public!replacing! !
!StProgramNode categoriesFor: #replaceMethodSource:!public!replacing! !
!StProgramNode categoriesFor: #replaceNode:withNode:!public!replacing! !
!StProgramNode categoriesFor: #replaceSourceFrom:!private!replacing! !
!StProgramNode categoriesFor: #replaceSourceWith:!private!replacing! !
!StProgramNode categoriesFor: #replaceWith:!public!replacing! !
!StProgramNode categoriesFor: #selfMessages!public!querying! !
!StProgramNode categoriesFor: #superMessages!public!querying! !

!StReturnNode methodsFor!

copyInContext: aDictionary 
	^(self class new)
		value: (value copyInContext: aDictionary);
		yourself!

equalTo: anObject withMapping: aDictionary 
	^self class = anObject class 
		and: [self value equalTo: anObject value withMapping: aDictionary]!

match: aNode inContext: aDictionary 
	aNode class == self class ifFalse: [^false].
	^value match: aNode value inContext: aDictionary!

replaceNode: aNode withNode: anotherNode 
	value == aNode ifTrue: [self value: anotherNode]! !
!StReturnNode categoriesFor: #copyInContext:!matching!public! !
!StReturnNode categoriesFor: #equalTo:withMapping:!comparing!public! !
!StReturnNode categoriesFor: #match:inContext:!matching!public! !
!StReturnNode categoriesFor: #replaceNode:withNode:!public!replacing! !

!StSequenceNode methodsFor!

addNode: aNode
	(statements notEmpty and: [statements last isReturn])
		ifTrue: [self error: 'Cannot add statement after return node'].
	statements := statements asOrderedCollection
				add: aNode;
				yourself.
	aNode parent: self.
	^aNode!

addNode: aNode before: anotherNode
	| index |
	index := self indexOfNode: anotherNode.
	index == 0 ifTrue: [^self addNode: aNode].
	statements := statements asOrderedCollection
				add: aNode beforeIndex: index;
				yourself.
	aNode parent: self.
	^aNode!

addNodeFirst: aNode
	statements := statements asOrderedCollection
				addFirst: aNode;
				yourself.
	aNode parent: self.
	^aNode!

addNodes: aCollection
	(statements notEmpty and: [statements last isReturn])
		ifTrue: [self error: 'Cannot add statement after return node'].
	statements := statements asOrderedCollection
				addAll: aCollection;
				yourself.
	aCollection do: [:each | each parent: self].
	^aCollection!

addNodes: aCollection before: anotherNode
	aCollection do: [:each | self addNode: each before: anotherNode].
	^aCollection!

addNodesFirst: aCollection
	statements := statements asOrderedCollection
				addAllFirst: aCollection;
				yourself.
	aCollection do: [:each | each parent: self].
	^aCollection!

addReturn
	| node |
	statements isEmpty ifTrue: [^nil].
	statements last isReturn ifTrue: [^statements last].
	node := StReturnNode value: statements last.
	statements at: statements size put: node.
	node parent: self.
	^node!

addSelfReturn
	| node |
	self lastIsReturn ifTrue: [^self].
	node := RBReturnNode value: (RBVariableNode named: 'self').
	self addNode: node!

addTemporariesNamed: aCollection 
	aCollection do: [:each | self addTemporaryNamed: each]!

addTemporaryNamed: aString 
	| variableNode |
	variableNode := RBVariableNode named: aString.
	variableNode parent: self.
	temporaries := temporaries copyWith: variableNode!

copyInContext: aDictionary 
	^(self class new)
		temporaries: (self copyList: temporaries inContext: aDictionary);
		statements: (self copyList: statements inContext: aDictionary);
		yourself!

equalTo: anObject withMapping: aDictionary 
	self class = anObject class ifFalse: [^false].
	self statements size == anObject statements size ifFalse: [^false].
	1 to: self statements size
		do: 
			[:i | 
			((self statements at: i) equalTo: (anObject statements at: i)
				withMapping: aDictionary) ifFalse: [^false]].
	aDictionary values asSet size = aDictionary size ifFalse: [^false].	"Not a one-to-one mapping"
	self temporaries
		do: [:each | aDictionary removeKey: each name ifAbsent: []].
	^true!

indexOfNode: aNode
	"Try to find the node by first looking for ==, and then for ="

	| index |
	(index := statements identityIndexOf: aNode) == 0 ifTrue: [index := statements indexOf: aNode].
	^index!

match: aNode inContext: aDictionary 
	self class == aNode class ifFalse: [^false].
	^(self 
		matchList: temporaries
		against: aNode temporaries
		inContext: aDictionary) and: 
				[self 
					matchList: statements
					against: aNode statements
					inContext: aDictionary]!

references: aVariableName 
	^statements anySatisfy: [:each | each references: aVariableName]!

removeDeadCode
	(self isUsed ifTrue: [statements size - 1] ifFalse: [statements size]) to: 1
		by: -1
		do: 
			[:i | 
			(statements at: i) isImmediateNode 
				ifTrue: 
					[self clearReplacements.
					statements removeAtIndex: i]].
	super removeDeadCode!

removeNode: aNode
	self replaceNode: aNode withNodes: #()!

removeTemporaryNamed: aName 
	temporaries := temporaries reject: [:each | each name = aName]!

replaceNode: aNode withNode: anotherNode 
	self statements: (statements 
				collect: [:each | each == aNode ifTrue: [anotherNode] ifFalse: [each]]).
	self temporaries: (temporaries 
				collect: [:each | each == aNode ifTrue: [anotherNode] ifFalse: [each]])!

replaceNode: aNode withNodes: aCollection 
	| index newStatements |
	self clearReplacements.
	index := self indexOfNode: aNode.
	newStatements := OrderedCollection new: statements size + aCollection size.
	1 to: index - 1 do: [:i | newStatements add: (statements at: i)].
	newStatements addAll: aCollection.
	index + 1 to: statements size
		do: [:i | newStatements add: (statements at: i)].
	aCollection do: [:each | each parent: self].
	statements := newStatements! !
!StSequenceNode categoriesFor: #addNode:!adding nodes!public! !
!StSequenceNode categoriesFor: #addNode:before:!adding nodes!public! !
!StSequenceNode categoriesFor: #addNodeFirst:!adding nodes!public! !
!StSequenceNode categoriesFor: #addNodes:!adding nodes!public! !
!StSequenceNode categoriesFor: #addNodes:before:!adding nodes!public! !
!StSequenceNode categoriesFor: #addNodesFirst:!adding nodes!public! !
!StSequenceNode categoriesFor: #addReturn!accessing!public! !
!StSequenceNode categoriesFor: #addSelfReturn!adding nodes!public! !
!StSequenceNode categoriesFor: #addTemporariesNamed:!adding nodes!public! !
!StSequenceNode categoriesFor: #addTemporaryNamed:!adding nodes!public! !
!StSequenceNode categoriesFor: #copyInContext:!matching!public! !
!StSequenceNode categoriesFor: #equalTo:withMapping:!comparing!public! !
!StSequenceNode categoriesFor: #indexOfNode:!helpers!private! !
!StSequenceNode categoriesFor: #match:inContext:!matching!public! !
!StSequenceNode categoriesFor: #references:!public!testing! !
!StSequenceNode categoriesFor: #removeDeadCode!public!replacing! !
!StSequenceNode categoriesFor: #removeNode:!public!replacing! !
!StSequenceNode categoriesFor: #removeTemporaryNamed:!accessing!public! !
!StSequenceNode categoriesFor: #replaceNode:withNode:!public!replacing! !
!StSequenceNode categoriesFor: #replaceNode:withNodes:!public!replacing! !

"End of package definition"!

